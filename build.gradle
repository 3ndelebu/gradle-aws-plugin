// -*- coding: utf-8; mode: groovy -*-

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.7.0'
	}
}

plugins {
    id 'com.jfrog.artifactory' version "3.0.1"
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'license'

dependencies {
    compile 'com.google.guava:guava:18.0'
}

defaultTasks 'clean', 'build'

group = 'TouchType'
version = '0.13-SNAPSHOT'



ext {
	artifactId = 'gradle-aws-plugin'
	defaultEncoding = 'UTF-8'
}

// ======== create source and javadoc bundles ========

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

configurations {
	deployerJars
}

// ======== library versions ========
ext {
	awsJavaSdkVersion = '1.9.0'
	groovyVersion = '2.3.7'
	junitVersion = '4.+'
	hamcrestVersion = '1.3'
	mockitoCoreVersion = '1.9.5'
}

repositories {
	mavenCentral()
}

dependencies {
	compile gradleApi()
	compile "org.codehaus.groovy:groovy-all:$groovyVersion"
	compile "com.amazonaws:aws-java-sdk:$awsJavaSdkVersion"
	compile 'commons-codec:commons-codec:1.8'
	
	// tests
	testCompile "junit:junit:$junitVersion"
	testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
	testCompile "org.mockito:mockito-core:$mockitoCoreVersion"

	deployerJars "org.springframework.build:aws-maven:4.7.0.RELEASE"
}

// ======== deploy artifacts ========
def releasePrefix = project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
              username System.env.ARTIFACTORY_USERNAME
              password System.env.ARTIFACTORY_PASSWORD
            }
            url "http://touchtype.artifactoryonline.com/touchtype/${releasePrefix}-internal"
        }
    }
}

// ======== wrapper ========
task wrapper(type: Wrapper) {
	gradleVersion = '2.1'
}

// ======== License =======
license {
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	header file('copyright/HEADER')
	strictCheck true
}

// ======== IDE ========
eclipse {
	project {
		buildCommand 'org.eclipse.jdt.core.javabuilder'
		buildCommand 'org.springframework.ide.eclipse.core.springbuilder'
		buildCommand 'net.sf.eclipsecs.core.CheckstyleNature'
		buildCommand 'edu.umd.cs.findbugs.plugin.eclipse.findbugsNature'
		natures 'org.eclipse.jdt.core.javanature',
				'org.springsource.ide.eclipse.gradle.core.nature',
				'org.springframework.ide.eclipse.core.springnature',
				'net.sf.eclipsecs.core.CheckstyleBuilder',
				'edu.umd.cs.findbugs.plugin.eclipse.findbugsBuilder'
	}
	classpath {
		defaultOutputDir = file('build/classes')
		containers = [
			'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7',
			'org.springsource.ide.eclipse.gradle.classpathcontainer'  // Gradle IDE classpath container
		]
		file {
			// exclude jar entries from .classpath
			whenMerged { classpath ->
				classpath.configure classpath.entries.grep { entry ->
					!(entry instanceof org.gradle.plugins.ide.eclipse.model.Library)
				}
				classpath.entries.findAll {
					it instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder && it.path.startsWith("src/test/")
				}*.output = "build/test-classes"
			}
		}
		downloadSources = true
		downloadJavadoc = true
	}
}
